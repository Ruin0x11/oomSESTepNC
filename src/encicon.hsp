// interface.bmpがあるバッファ(ID: 3)の (0, 800) から (1440, 126) の長方形をエンチャアイコンに当てる



// The size of hash table should be a prime number.
#const ICON_TABLE_SIZE 503
#const ICON_TABLE_REHASH 17



#deffunc encIcon_addIcon str __filename, int __encType, int __lowerBoundary, int __upperBoundary, int __flag
	hash@encicon = __encType \ ICON_TABLE_SIZE
	repeat
		if (iconTableData@encicon(0, hash@encicon) == 0) {
			// empty
			break
		} else {
			// collision!
			hash@encicon = (hash@encicon + ICON_TABLE_REHASH) \ ICON_TABLE_SIZE
		}
	loop
	iconTableData@encicon(0, hash@encicon) = __encType
	iconTableData@encicon(1, hash@encicon) = __lowerBoundary
	iconTableData@encicon(2, hash@encicon) = __upperBoundary
	iconTableData@encicon(3, hash@encicon) = __flag
	// Load graphics
	pos 18 * (hash@encicon \ 80), 800 + 18 * (hash@encicon / 80)
	picload_ exedir + "user\\graphic\\" + __filename, 1
	return



#defcfunc encIcon_find int __encType, int __power
	hash@encicon = __encType \ ICON_TABLE_SIZE
	repeat
		if (iconTableData@encicon(0, hash@encicon) == 0) {
			hash@encicon = -1
			break
		}

		if (iconTableData@encicon(0, hash@encicon) == __encType) {
			// check power
			matched@encicon = 1
			if (iconTableData@encicon(3, hash@encicon) & 1) { // has lower boundary
				if (__power < iconTableData@encicon(1, hash@encicon)) {
					matched@encicon = 0
				}
			}
			if (iconTableData@encicon(3, hash@encicon) & 2) { // has upper boundary
				if (iconTableData@encicon(2, hash@encicon) < __power) {
					matched@encicon = 0
				}
			}
			if (matched@encicon) {
				break
			}
		}

		// rehash and search next
		hash@encicon = (hash@encicon + ICON_TABLE_REHASH) \ ICON_TABLE_SIZE
	loop
	return hash@encicon



// エンチャアイコンファイルの存在を確認し、それを元にデータを作成
// enchantIcon_<type>_<lowerBoundary>_<upperBoundary>@comment.bmp
#deffunc encIcon_loadEncIcons
	// Save current draw position.
	saveGInfoCX@encicon = ginfo_cx
	saveGInfoCY@encicon = ginfo_cy
	saveGInfoSel@encicon = ginfo_sel

	// Allocate table.
	dim iconTableData@encicon, 5, ICON_TABLE_SIZE

	// interface.bmp
	gsel 3

	dirlist files@encicon, exedir + "user\\graphic\\enchantIcon_*.bmp", 3

	notesel files@encicon
	repeat notemax
		noteget file@encicon, cnt
		// Extract part of filename.
		file@encicon = getpath(file@encicon, 8)
		s@encicon = file@encicon

		// Delete the trailing comment.
		atPos@encicon = instr(s@encicon, 0, "@")
		if (atPos@encicon != -1) {
			s@encicon = strmid(s@encicon, 0, atPos@encicon)
		}

		// Strip the prefix.
		s@encicon = strmid(s@encicon, strlen("enchantIcon_"), 999)

		// <type>
		encType@encicon = int(s@encicon)

		// <lowerBoundary>
		underscorePos@encicon = instr(s@encicon, 0, "_")
		if (underscorePos@encicon != -1) {
			s@encicon = strmid(s@encicon, underscorePos@encicon + 1, 999)
		}
		if (underscorePos@encicon != -1 & s@encicon != "" & strmid(s@encicon, 0, 1) != "_") {
			lowerBoundary@encicon = int(s@encicon)
			hasLowerBoundary@encicon = 1
		} else {
			lowerBoundary@encicon = 0
			hasLowerBoundary@encicon = 0
		}

		// <upperBoundary>
		underscorePos@encicon = instr(s@encicon, 0, "_")
		if (underscorePos@encicon != -1) {
			s@encicon = strmid(s@encicon, underscorePos@encicon + 1, 999)
		}
		if (underscorePos@encicon != -1 & s@encicon != "") {
			upperBoundary@encicon = int(s@encicon)
			hasUpperBoundary@encicon = 2
		} else {
			upperBoundary@encicon = 0
			hasUpperBoundary@encicon = 0
		}

		encIcon_addIcon file@encicon, encType@encicon, lowerBoundary@encicon, upperBoundary@encicon, hasLowerBoundary@encicon + hasUpperBoundary@encicon
	loop
	noteunsel

	// Restore.
	pos saveGInfoCX@encicon, saveGInfoCY@encicon
	gsel saveGInfoSel@encicon
	return



#deffunc encIcon_draw int __item, int __x, int __y
	xOffset@encicon = 0
	repeat 15
		if (iEnchantmentId(__item, cnt) == 0) {
			continue
		}
		hash@encicon = encIcon_find(iEnchantmentId(__item, cnt), iEnchantmentPower(__item, cnt))
		if (hash@encicon == -1) {
			continue
		}

		pos __x + xOffset@encicon, __y
		gcopy 3, 18 * (hash@encicon \ 80), 800 + 18 * (hash@encicon / 80), 18, 18
		xOffset@encicon += 18
	loop
	return
